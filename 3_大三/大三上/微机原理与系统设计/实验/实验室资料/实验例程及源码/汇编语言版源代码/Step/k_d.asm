COM_8255	EQU	0273H		;8255控制口
PA_8255		EQU	0270H
PB_8255		EQU	0271H
PC_8255		EQU	0272H
IO_273		EQU	0230H

PUBLIC	InitKeyDisplay, GetKeyA, DisPlay8
PUBLIC	F1

_DATA		SEGMENT	WORD PUBLIC 'DATA'
F1		DB	?
buffer          DB      8 DUP(?)					;8个字节显示缓冲区
;keyoffset	DW	?						;键值存放在显示缓冲区位置
SEG_TAB:	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H
		DB	080H,090H,088H,083H,0C6H,0A1H,086H,08EH 
		DB	0FFH,0BFH
SEG_TABA:	DB	03FH,006H,05BH,04FH,066H,06DH,07DH,007H
		DB	07FH,06FH,077H,07CH,039H,05EH,079H,071H 
		DB	000H,040H
_DATA		ENDS		
			
CODE		SEGMENT	WORD PUBLIC 'CODE'	
		ASSUME	CS:CODE, DS:_DATA

;CY =1,有键,键值在AL中;CY=0,没有按键
GetKeyA		PROC	NEAR
		CALL	SCAN_KEY
		RET
GetKeyA		ENDP

InitKeyDisplay	PROC	NEAR
		PUSH	AX
		PUSH	DX
		MOV	DX,COM_8255
		MOV	AL,81H;9H
		OUT	DX,AL			;PA、PB输出，PC输入
		POP	DX
		POP	AX
		RET
InitKeyDisplay	ENDP

DisPlay8	PROC	NEAR
;		PUSHF
		PUSH	ES
		PUSH	DI
		PUSH	SI
		PUSH	CX
		PUSH	DS
		POP	ES
		CLD
		LEA	DI,buffer
		MOV	CX,8
		REP	MOVSB
		POP	CX
		POP	SI
		POP	DI
		POP	ES
;		POPF
		CALL	DIR
		RET
DisPlay8	ENDP

DIR		PROC	NEAR
;		PUSHF
		PUSH	AX
		PUSH	BX
		PUSH	DX
		PUSH	SI
		CLD
		LEA	SI,buffer		;置显示缓冲器初值
		MOV	AH,0FEH
		LEA	BX,SEG_TAB
LD0:		LODSB
		MOV	DX,AX
		AND	AL,7FH
		XLAT				;取显示数据
		TEST	DL,80H			;最高位是1，需要显示小数点
		JZ	LD2
		AND	AL,7FH
LD2:		MOV	DX,PA_8255
		OUT	DX,AL			;段数据->8255 PA口
		INC	DX			;扫描模式->8255 PB口
		MOV	AL,AH
		OUT	DX,AL
		CALL	DL1			;延迟1ms
		MOV	DX,PB_8255
		MOV	AL,0FFH
		OUT	DX,AL
		TEST	AH,80H
		JZ	LD1
		ROL	AH,01H
		JMP	LD0
LD1:		POP	SI
		POP	DX
		POP	BX
		POP	AX
;		POPF
		RET
DIR		ENDP

DL1		PROC	NEAR			;延迟子程序
		PUSH	CX
		MOV	CX,500
		LOOP	$
		POP	CX
		RET
DL1		ENDP

SCAN_KEY	PROC	NEAR
KEYI:		PUSH	BX
		PUSH	DX
LK:		CALL	AllKey			;调用判有无闭合键子程序
		JNZ	LK1
		CALL	DIR			;调用显示子程序,延迟6ms
		JMP	LKK
LK1:		CALL	DIR
		CALL	AllKey			;调用判有无闭合键子程序
		JZ	LKK
		CALL	DIR
		CALL	AllKey			;调用判有无闭合键子程序
		JZ	LKK
LK2:		MOV	BL,0FEH		;R2
		MOV	BH,0		;R4
LK4:		MOV	DX,PB_8255
		MOV	AL,BL
		OUT	DX,AL
		INC	DX
		IN	AL,DX
		TEST	AL,01H
		JNZ	LONE
		XOR	AL,AL			;0行有键闭合
		JMP	LKP			
LONE:		TEST	AL,02H
		JNZ	NEXT
		MOV	AL,08H			;1行有键闭合
LKP:		ADD	BH,AL
LK3:		CALL	DIR		;判断释放否
		CALL	AllKey
		JNZ	LK3
		MOV	AL,BH			;键号->AL
		STC
		JMP	KND
NEXT:		INC	BH			;列计数器加1
		TEST	BL,80H
		JZ	LKK			;判是否已扫到最后一列
		ROL	BL,01H
		JMP	LK4
LKK:		CLC
KND:		POP	DX
		POP	BX
		RET
SCAN_KEY	ENDP

AllKey		PROC	NEAR
		MOV	DX,PB_8255
		XOR	AL,AL
		OUT	DX,AL			;全"0"->扫描口
		INC	DX
		IN	AL,DX			;读键状态
		NOT	AL
		AND	AL,03H			;取低二位
		RET
AllKey		ENDP
				
CODE		ENDS
		END
